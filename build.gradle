plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    compileOnly("org.bstats:bstats-bukkit:3.0.1")
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly files('libraries/tebex-bukkit-2.0.0.jar')
    compileOnly files('libraries/BuycraftX.jar')
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

shadowJar {
    relocate('okhttp3', 'org.metadevs.buycraftapi.libraries.okhttp3')
    relocate('okio', 'org.metadevs.buycraftapi.libraries.okio')
    relocate('kotlin', 'org.metadevs.buycraftapi.libraries.kotlin')
    relocate("org.bstats", "me.clip.placeholderapi.metrics")
    relocate('org.jetbrains.annotations', 'org.metadevs.buycraftapi.libraries.jetbrains.annotations')

    minimize()

    archiveFileName = 'BuyCraftAPI-' + version + '.jar'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'BuyCraftAPI',
                'Implementation-Version': version,
                'Implementation-Vendor': 'BuyCraft',
                'Implementation-Vendor-Id': 'org.metadevs.buycraftapi',
                'Specification-Title': 'BuyCraftAPI',
                'Specification-Version': version,
                'Specification-Vendor': 'BuyCraft',
                'Main-Class': 'org.metadevs.buycraftapi.BuyCraftAPI'
        )
    }
}

build.dependsOn(shadowJar)

group = 'org.metadevs.buycraftapi'
description = 'BuyCraftAPI'
java.sourceCompatibility = JavaVersion.VERSION_17

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
